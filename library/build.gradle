apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    defaultConfig {
        minSdkVersion 11
        targetSdkVersion 26
        versionCode 1
        versionName "$version"
    }
}

archivesBaseName = 'unicorn'

def repoDesc = 'A funny Unicorn widget to include in your layouts as a gadget'
def repoName = 'android-unicorn'
def repoUrl = "https://github.com/gmazzo/$repoName"

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = repoName
        desc = repoDesc
        vcsUrl = "${repoUrl}.git"
        githubRepo = repoUrl.replaceFirst('\\Qhttps://github.com/\\E', '');
        githubReleaseNotesFile = 'README.md'
        licenses = ['Public Domain']
        labels = ['aar', 'android', 'unicorn']
        publicDownloadNumbers = true
    }
}

dependencies {
    compile 'com.android.support:support-annotations:25.1.1'
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name repoDesc
                url repoUrl
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += configurations.compile
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}
